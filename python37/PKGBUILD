# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Jason Chu <jason@archlinux.org>

_py_ver=37
pkgname=python$_py_ver
pkgver=3.7.7
pkgrel=1
_pybasever=${pkgver%.*}
pkgdesc='Next generation of the python high-level scripting language'
arch=('x86_64')
license=('custom')
url='https://www.python.org/'
depends=('expat' 'bzip2' 'gdbm' 'openssl' 'libffi' 'zlib' 'libnsl')
makedepends=('tk' 'sqlite' 'valgrind' 'bluez-libs' 'mpdecimal' 'llvm' 'gdb' 'xorg-server-xvfb')
optdepends=('python-setuptools'
            'python-pip'
            'sqlite'
            'mpdecimal: for decimal'
            'xz: for lzma'
            'tk: for tkinter')
provides=('python3')
replaces=('python3')
source=("https://www.python.org/ftp/python/${pkgver%rc*}/Python-$pkgver.tar.xz"{,.asc})
sha512sums=('ddc838a7b0c442c2e465616f20231f2b703ed6b69ed2dc17858aac8760814fdf7cff43d350d359300e47b6bb1f0bd38c31126b855e423a3a65ed06a8fa16d136'
            'SKIP')
validpgpkeys=('0D96DF4D4110E5C43FBFB17F2D347EA6AA65421D')  # Ned Deily (Python release signing key) <nad@python.org>

prepare() {
  cd Python-$pkgver

  # https://bugs.python.org/issue34587
  sed -i -e 's|testCongestion|disabled_&|' Lib/test/test_socket.py

  # FS#23997
  sed -i -e 's|^#.* /usr/local/bin/python|#!/usr/bin/python|' Lib/cgi.py

  # Speed up LTO
  sed -i -e 's|-flto |-flto=4 |g' configure configure.ac

  # Ensure that we are using the system copy of various libraries (expat, libffi, and libmpdec),
  # rather than copies shipped in the tarball
  rm -r Modules/expat
  rm -r Modules/_ctypes/{darwin,libffi}*
  rm -r Modules/_decimal/libmpdec
}

build() {
  cd Python-$pkgver

  # PGO should be done with -O3
  CFLAGS="${CFLAGS/-O2/-O3}"

  # Disable bundled pip & setuptools
  ./configure --prefix=/usr \
              --enable-shared \
              --with-threads \
              --with-computed-gotos \
              --with-lto \
              --enable-ipv6 \
              --with-system-expat \
              --with-dbmliborder=gdbm:ndbm \
              --with-system-ffi \
              --with-system-libmpdec \
              --enable-loadable-sqlite-extensions \
              --without-ensurepip

#              --enable-optimizations \

  # Obtain next free server number for xvfb-run; this even works in a chroot environment.
  export servernum=99
  while ! xvfb-run -a -n "$servernum" /bin/true 2>/dev/null; do servernum=$((servernum+1)); done

  LC_CTYPE=en_US.UTF-8 xvfb-run -s '-screen 0 1280x720x24 -ac +extension GLX' -a -n "$servernum" make EXTRA_CFLAGS="$CFLAGS"
}

#check() {
#  # test_gdb is expected to fail with LTO
#  # test_idle, test_tk, test_ttk_guionly segfaults since 3.6.5
#
#  # https://bugs.python.org/issue34022
#  # test_cmd_line_script, test_compileall, test_importlib,
#  # test_multiprocessing_main_handling, test_py_compile, test_runpy
#
#  cd Python-${pkgver}
#
#  # Obtain next free server number for xvfb-run; this even works in a chroot environment.
#  export servernum=99
#  while ! xvfb-run -a -n "$servernum" /bin/true 2>/dev/null; do servernum=$((servernum+1)); done
#
#  LD_LIBRARY_PATH="$srcdir/Python-$pkgver":$LD_LIBRARY_PATH \
#  LC_CTYPE=en_US.UTF-8 xvfb-run -s "-screen 0 1280x720x24 -ac +extension GLX" -a -n "$servernum" \
#    "$srcdir/Python-$pkgver/python" -m test.regrtest -v -uall -x test_gdb -x test_idle -x test_tk -x test_ttk_guionly \
#    -x test_cmd_line_script -x test_compileall -x test_importlib -x test_multiprocessing_main_handling -x test_py_compile -x test_runpy \
#    -x test_httplib
#}

package() {
  cd Python-${pkgver}

  # Hack to avoid building again
  sed -i 's/^all:.*$/all: build_all/' Makefile

  # PGO should be done with -O3
  CFLAGS="${CFLAGS/-O2/-O3}"

  make DESTDIR="$pkgdir" EXTRA_CFLAGS="$CFLAGS" install

  # some useful "stuff" FS#46146
  install -dm 755 "$pkgdir"/usr/lib/python$_pybasever/Tools/{i18n,scripts}
  install -m 755 Tools/i18n/{msgfmt,pygettext}.py "$pkgdir"/usr/lib/python$_pybasever/Tools/i18n/
  install -m 755 Tools/scripts/{README,*py} "$pkgdir"/usr/lib/python$_pybasever/Tools/scripts/

  # fix conflicts with python
  rm -f "$pkgdir"/usr/bin/pyvenv{,3}
  rm -f "$pkgdir"/usr/bin/2to3{,3}
  rm -f "$pkgdir"/usr/bin/idle{,3}
  rm -f "$pkgdir"/usr/bin/pydoc{,3}
  rm -f "$pkgdir"/usr/bin/python{,3}
  rm -f "$pkgdir"/usr/bin/python{,3}-config
  rm -f "$pkgdir"/usr/lib/libpython3.so
  rm -f "$pkgdir"/usr/lib/pkgconfig/python3.pc
  rm -f "$pkgdir"/usr/share/man/man1/python3.1

  # License
  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
